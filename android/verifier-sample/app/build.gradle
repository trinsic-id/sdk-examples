plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "id.trinsic.android"
        minSdk 29
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'id.trinsic.android'
    lint {
        abortOnError false
    }
}

def urlFile = { url, name ->
    File file = new File("$buildDir/download/${name}")
    file.parentFile.mkdirs()
    if (!file.exists()) {
        new URL(url).withInputStream { downloadStream ->
            file.withOutputStream { fileOut ->
                fileOut << downloadStream
            }
        }
    }
    files(file.absolutePath)
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'io.grpc:grpc-netty-shaded:1.49.2'
    implementation 'io.grpc:grpc-okhttp:1.49.0'
    implementation 'io.grpc:grpc-protobuf:1.49.2'
    implementation 'io.grpc:grpc-stub:1.49.2'
    implementation 'net.java.dev.jna:jna:5.13.0@aar'

    implementation urlFile('https://github.com/trinsic-id/okapi/releases/download/v1.6.0/trinsic-okapi-1.6.2.aar', 'trinsic-okapi-1.6.2.aar')

//  Use the below implementation line if you're pulling the dependency from a local file
//  implementation files("C:\\work\\okapi\\java\\build\\libs\\trinsic-okapi-1.6.1.aar")
    implementation urlFile('https://github.com/trinsic-id/sdk/releases/download/v1.9.0/trinsic-services-1.9.0.jar', 'trinsic-services-1.9.0.jar')
}